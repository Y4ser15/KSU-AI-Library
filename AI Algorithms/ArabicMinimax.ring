حمل "../ArabicFunctions.ring"
حمل "../ArabicClasses.ring"
حمل "AINode.ring"
حمل "../Tests/Number_problem.ring"


توليد = 0

دالة بحث_ادنى_واعلى (مشكلة, جملة)

	الخلية_المدخلة = انشاء خلية_الذكاء_الاصطناعي
	الخلية_المدخلة.الحالة = مشكلة.الحالة_البدائية

	اذا (جملة = "اعلى") او (جملة = "اقصى")
		ارجع اعلى(مشكلة, الخلية_المدخلة)
	والااذا (جملة = "ادنى") او (جملة = "اقل")
		ارجع ادنى(مشكلة, الخلية_المدخلة)
	والا
		ارجع اعلى(مشكلة, الخلية_المدخلة)
	نهاية


دالة اعلى (مشكلة, الخلية_المدخلة)

	الابناء = مشكلة.خطوة(الخلية_المدخلة.الحالة)
	اعلى_قيمة = -999999999999999

	اذا (الابناء = فراغ)
		ارجع الخلية_المدخلة.الحالة
	نهاية

	كررمن ر=1 الى طول(الابناء)
		توليد++
		خلية = انشاء خلية_الذكاء_الاصطناعي{ الحالة = الابناء[ر] }
		خلية.الحالة = ادنى(مشكلة, خلية)

		اذا (اعلى_قيمة < خلية.الحالة)
			اعلى_قيمة = خلية.الحالة
		نهاية
	التالي

	ارجع اعلى_قيمة


دالة ادنى (مشكلة, الخلية_المدخلة)

	الابناء = مشكلة.خطوة(الخلية_المدخلة.الحالة)
	اقل_قيمة = 999999999999999

	اذا (الابناء = فراغ)
		ارجع الخلية_المدخلة.الحالة
	نهاية

	كررمن ر=1 الى طول(الابناء)
		توليد++
		خلية = انشاء خلية_الذكاء_الاصطناعي{ الحالة = الابناء[ر] }
		خلية.الحالة = اعلى(مشكلة, خلية)

		اذا (اقل_قيمة > خلية.الحالة)
			اقل_قيمة = خلية.الحالة
		نهاية
	التالي

	ارجع اقل_قيمة


دالة طباعة_بحث_ادنى_واعلى (مشكلة, جملة)


	وقت_البداية = مؤقت()
	حل_المشكلة = بحث_ادنى_واعلى(مشكلة, جملة)
	وقت_البحث = (مؤقت() - وقت_البداية) / دقات_بالثانية()

	اطبع حل_المشكلة
	اطبع سطر + "تم توليد: " + توليد + " خلية" + سطر
	اطبع "خلال: " +  وقت_البحث +" ثانية" + سطر

	توليد = 0


